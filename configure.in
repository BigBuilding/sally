# Sally - A Library for String Features and String Kernels
# Copyright (C) 2010 Konrad Rieck (konrad@mlsec.org)

AC_INIT([sally], [0.1.0], [konrad@mlsec.org])
AC_PREFIX_DEFAULT("/usr/local")

# Init automake & config.h
AM_INIT_AUTOMAKE
AM_CONFIG_HEADER([config.h])

# Check for important programs
AC_PROG_CC
AC_PROG_LD
AC_PROG_INSTALL

# Libtool stuff
AC_CONFIG_MACRO_DIR(m4)
AC_PROG_LIBTOOL

# Set GCC and C99 flags if present
if test "$GCC" = "yes" ; then
   CFLAGS="$CFLAGS -std=c99 -Wall -fPIC"
fi

AC_ARG_ENABLE(openmp, 
    [  --enable-openmp         enable support for OpenMP ],
    [ 
	if test "x$enableval" = "xyes" ; then
		SALLY_OPENMP=yes
		AC_DEFINE(SALLY_OPENMP, 1, [Enable support for OpenMP])
        else
		SALLY_OPENMP=no
        fi
    ], [ SALLY_OPENMP=no ]
)

# OpenMP support
AX_OPENMP([
   HAVE_OPENMP=yes
   AC_DEFINE(HAVE_OPENMP, 1, [Enable OpenMP support])
],[
   HAVE_OPENMP=no
])

if test "x$SALLY_OPENMP" = "xyes" ; then
   if test "x$HAVE_OPENMP" = "xno" ; then
      AC_MSG_ERROR([OpenMP support enabled but OpenMP not available])
   fi
   CFLAGS="$CFLAGS $OPENMP_CFLAGS"
fi 

# Check headers
AC_CHECK_HEADERS([getopt.h string.h strings.h])

# Check functions
AC_CHECK_FUNC(log2, AC_DEFINE(HAVE_FUNC_LOG2, 1, 
       [Define to 1 if you have the function log2]))

AC_SUBST([INCLUDES])
AC_CONFIG_FILES([ 
   Makefile \
   src/Makefile \
   doc/Makefile \
   tests/Makefile \
])

AC_OUTPUT_COMMANDS([],[
   SALLY_OPENMP=$SALLY_OPENMP
])

AC_OUTPUT
